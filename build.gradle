/*
 * Copyright (c) 2008 - 2012 10gen, Inc. <http://10gen.com>
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def configDir = new File(rootDir, 'config')

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: 'maven'


    group = 'org.mongodb'
    version = '3.0.0-SNAPSHOT'
    sourceCompatibility = '1.5'
    targetCompatibility = '1.5'

    checkstyle {
        configFile = new File("$configDir/checkstyle.xml")
    }

    repositories {
        mavenCentral()
    }
    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main'
            }
        }
        test {
            java {
                srcDir 'src/test'
            }
        }
    }
}

project(':driver') {
    sourceSets {
        acceptance {
            java {
                srcDir 'src/acceptancetest'
            }
        }
    }

    jar {
        baseName = 'mongo-driver'
        dependsOn classes
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    }

    task acceptanceTest(type: Test) {
        testClassesDir = sourceSets.acceptance.output.classesDir
        classpath = sourceSets.acceptance.runtimeClasspath
    }

    dependencies {
        compile project(':bson')
        acceptanceCompile group: 'junit', name: 'junit', version: '4.11'
        acceptanceCompile sourceSets.main.runtimeClasspath
    }
}

project(':driver-compat') {
    jar {
        baseName = 'mongo-driver-compat'
        dependsOn classes
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    }

    dependencies {
        compile project(':driver')
    }
}


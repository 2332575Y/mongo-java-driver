/*
 * Copyright (c) 2008 - 2013 MongoDB Inc., Inc. <http://mongodb.com>
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
}

idea {
    project {
        languageLevel = '1.6'
        ipr {
            withXml { provider ->
                def mapping = provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping
                mapping.@vcs = 'Git'
                mapping.@directory = '$PROJECT_DIR$'

                def node = provider.node.component.find { it.@name == 'CheckStyle-IDEA-Module' } ?: provider.node.appendNode('component')
                node.replaceNode {
                    component(name: 'CheckStyle-IDEA') {
                        option(name: 'configuration') {
                            map {
                                entry(key: 'active-configuration', value: 'FILE:$PROJECT_DIR$/config/checkstyle-lite.xml:Lite')
                            }
                        }
                    }
                }

                node = provider.node.component.find { it.@name == 'CopyrightManager' } ?: provider.node.appendNode('component')
                node.replaceNode {
                    component(name: 'CopyrightManager', default: 'MongoDB') {
                        copyright {
                            option(name: 'notice', value: 'Copyright (c) 2008 - \$today.year MongoDB Inc., ' +
                                                          'Inc. <http://mongodb.com>\n\nLicensed under the Apache License, ' +
                                                          'Version 2.0 (the "License");\nyou may not use this file except in compliance ' +
                                                          'with the License.\nYou may obtain a copy of the License at\n\n  ' +
                                                          'http://www.apache.org/licenses/LICENSE-2.0\n\nUnless required by applicable ' +
                                                          'law or agreed to in writing, software\ndistributed under the License is ' +
                                                          'distributed on an "AS IS" BASIS,\nWITHOUT WARRANTIES OR CONDITIONS OF ANY ' +
                                                          'KIND, either express or implied.\nSee the License for the specific language ' +
                                                          'governing permissions and\nlimitations under the License.')
                            option(name: 'keyword', value: 'Copyright')
                            option(name: 'allowReplaceKeyword', value: '')
                            option(name: 'myName', value: '10gen')
                            option(name: 'myLocal', value: true)
                        }
                        module2copyright {
                            element(module: 'Project Files', copyright: 'MongoDB')
                        }
                        LanguageOptions(name: 'XML') {
                            option(name: 'fileTypeOverride', value: 1)
                            option(name: 'prefixLines', value: false)
                        }
                    }
                }
            }
            beforeMerged { project ->
                project.modulePaths.clear()
            }
        }
    }
}